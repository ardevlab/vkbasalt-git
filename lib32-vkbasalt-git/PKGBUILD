#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: gee
#Credits: Zeioth <zeioth@hotmail.com>
#Credits: FabioLolix <fabio.loli@disroot.org>
#Credits: yochananmarqos, bpierre, PedroHLC, rodrigo21

pkgname=lib32-vkbasalt-git
pkgdesc='A Vulkan post processing layer to enhance the visual graphics of games (32-bit) (git version)'
pkgver=0.3.2.10.r0.g4f97f09
pkgrel=1
arch=(i686 x86_64)
url='https://github.com/DadSchoorse/vkBasalt'
license=(zlib)
makedepends=(meson ninja glslang libx11 lib32-libx11 spirv-headers vulkan-headers gcc gcc-libs lib32-gcc-libs git)
depends=(gcc-libs glslang libx11 lib32-gcc-libs lib32-libx11 vkbasalt-git)
optdepends=(reshade-shaders-git)
provides=(lib32-vkbasalt lib32-vkbasalt-git)
source=(git+https://github.com/DadSchoorse/vkBasalt.git)

pkgver(){
 cd "${srcdir}"/vkBasalt
 git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export LDFLAGS+=" -m32"
export ASFLAGS+=" --32"
export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"
export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  rm -rf "${srcdir}"/build

  local meson_options=(
    --cross-file lib32
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    --wrap-mode=nofallback
    -D prefix=/usr
    -D bindir=bin
    -D datadir=share
    -D includedir=include
    -D libdir=lib32
    -D sysconfdir=/etc
    -D with_so=true
    -D with_json=true
    -D append_libdir_vkbasalt=false
  )

  meson "${srcdir}"/vkBasalt "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build
}

package(){
  DESTDIR="${pkgdir}" meson install -C "${srcdir}"/build

  mv "${pkgdir}"/usr/share/vulkan/implicit_layer.d/vkBasalt.json "${pkgdir}"/usr/share/vulkan/implicit_layer.d/vkBasalt.i686.json
  sed -i 's|VK_LAYER_VKBASALT_post_processing|VK_LAYER_VKBASALT_post_processing_32|g' "${pkgdir}"/usr/share/vulkan/implicit_layer.d/vkBasalt.i686.json
  sed -i 's|libvkbasalt.so|/usr/lib32/libvkbasalt.so|g' "${pkgdir}"/usr/share/vulkan/implicit_layer.d/vkBasalt.i686.json

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/vkBasalt/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
